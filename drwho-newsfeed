#!/usr/bin/perl

use strict;
use warnings;
use feature 'say';

use Web::Query;
use DateTime;
use DateTime::Format::Strptime;
use XML::Feed;
use Encode 'decode';

my $start = 'https://www.doctorwho.tv/news/';

my $dp = DateTime::Format::Strptime->new(
  pattern => '%A %d %B %Y',
);

my $atom = XML::Feed->new('Atom');
$atom->title('Doctor Who - Latest News');
$atom->link($start);
$atom->id($start);
$atom->modified(DateTime->now);

wq($start)
  ->find('article')
  ->each(sub {
      my $entry = XML::Feed::Entry->new('Atom');
      $entry->title($_->find('h1')->text);
      $entry->author('BBC Doctor Who News');
      $entry->link($start . $_->find('a')->attr('href'));
      $entry->id($start . $_->find('a')->attr('href'));
      $entry->content('<p><img src="' .
                      $_->find('img')->attr('data-src') .
                      '"></p><p>' .
                      $_->find('p')->text .
                      '</p>');
      $entry->modified($dp->parse_datetime($_->find('span.date')->text));
      $atom->add_entry($entry);
    });

say $atom->as_xml;
